; Structure Example: Change the system's clock to the specified date and time. Use caution when
; changing to a date in the future as it may cause scheduled tasks to run prematurely!

SetSystemTime("20051008142211")  ; Pass it a timestamp (local, not UTC).

SetSystemTime(YYYYMMDDHHMISS)
; Sets the system clock to the specified date and time.
; Caller must ensure that the incoming parameter is a valid date-time stamp
; (local time, not UTC). Returns non-zero upon success and zero otherwise.
{
    ; Convert the parameter from local time to UTC for use with SetSystemTime().
    UTC_Delta -= %A_NowUTC%, Seconds  ; Seconds is more accurate due to rounding issue.
    UTC_Delta := Round(-UTC_Delta/60)  ; Round to nearest minute to ensure accuracy.
    YYYYMMDDHHMISS += %UTC_Delta%, Minutes  ; Apply offset to convert to UTC.

    VarSetCapacity(SystemTime, 16, 0)  ; This struct consists of 8 UShorts (i.e. 8*2=16).

    StringLeft, Int, YYYYMMDDHHMISS, 4    ; YYYY (year)
    NumPut(Int, SystemTime, 0, 2)
    StringMid, Int, YYYYMMDDHHMISS, 5, 2  ; MM (month of year, 1-12)
    NumPut(Int, SystemTime, 2, 2)
    StringMid, Int, YYYYMMDDHHMISS, 7, 2  ; DD (day of month)
    NumPut(Int, SystemTime, 6, 2)
    StringMid, Int, YYYYMMDDHHMISS, 9, 2  ; HH (hour in 24-hour time)
    NumPut(Int, SystemTime, 8, 2)
    StringMid, Int, YYYYMMDDHHMISS, 11, 2 ; MI (minute)
    NumPut(Int, SystemTime, 10, 2)
    StringMid, Int, YYYYMMDDHHMISS, 13, 2 ; SS (second)
    NumPut(Int, SystemTime, 12, 2)

    return DllCall("SetSystemTime", UInt, &SystemTime)
}